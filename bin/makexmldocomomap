#!/usr/bin/perl -w
use strict;
use warnings;
use utf8;
use LWP::Simple;
use XML::Simple ();
use Encode;

my $URL =
'http://www.nttdocomo.co.jp/service/imode/make/content/spec/screen_area/index.html';

do_task(@ARGV);

sub do_task {
    my $html = decode( 'cp932',  get($URL) );
    $html =~ s/(?:\r?\n)+/\n/g;
    $html =~ s/&mu;/myu/g;
    my $re = regexp();
    my %map;
    while ( $html =~ /$re/igs ) {
        my ( $model, $width, $height, $color, $depth ) = ( $1, $2, $3, $4, $5 );
        push @{ $map{terminal} },
          {
            model  => uc($model),
            width  => $width,
            height => $height,
            color  => $color eq 'カラー',
            depth  => $depth,
          };
    }
    output_code( \%map );
}

sub output_code {
    my ($map) = @_;
    my $xml = XML::Simple->new;
    printf <<'TEMPLATE', $xml->XMLout($map);
<?xml version="1.0" ?>
%s
TEMPLATE
}

sub regexp {
    return <<'RE';
<td><span class="txt">([A-Z]+\d+\w*\+?).*?</span></td>
<td><span class="txt">.*?(?:</span></td>)?
<td><span class="txt">.*?(?:</span></td>)?
<td><span class="txt">.*?(\d+)×(\d+).*?</span></td>
<td>.*?</td>
<td><span class="txt">(白黒|カラー)(?:.*?)(\d+)(?:色|階調)</span></td>
RE
}
